apply plugin: 'com.android.application'
apply plugin: 'realm-android'

ext.versionMajor = 00
ext.versionMinor = 01
ext.versionPatch = 00
ext.versionClassifier = ""
ext.isSnapshot = false
ext.minimumSdkVersion = 17
ext.minimumScreenSize = 1
ext.maximumScreenSize = 4

android {
    compileSdkVersion 27
    defaultConfig {
        applicationId "de.netalic.falcon"
        minSdkVersion project.ext.minimumSdkVersion
        targetSdkVersion 27
        versionCode generateVersionCode()
        versionName generateVersionName()
        flavorDimensions "default"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        releaseSign {
            def propsFile = rootProject.file('keystore.properties')
            if (propsFile.exists()) {
                def props = new Properties()
                props.load(new FileInputStream(propsFile))
                storeFile = file(props['storeFile'])
                storePassword = props['storePassword']
                keyAlias = props['keyAlias']
                keyPassword = props['keyPassword']
            }
        }
    }

    buildTypes {
        debug {
            debuggable true
            minifyEnabled false
            applicationIdSuffix ".debug"
            resValue "string", "app_name", "falcon-debug"
        }
        release {
            signingConfig signingConfigs.releaseSign
            debuggable false
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            applicationIdSuffix ".release"
            resValue "string", "app_name", "falcon-release"
        }
    }

    productFlavors {

        carrene_dev {
            buildConfigField "String", "WEB_SERVICE_URL", "\"https://sotp.bankmelli-iran.com\""
            buildConfigField "Integer", "WEB_SERVICE_PORT", "443"
            buildConfigField "Integer", "WEB_SERVICE_VERSION", "1"
            applicationIdSuffix ".carrene"
            resValue "string", "app_name", "InTouch Messenger"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

private Integer generateVersionCode() {
    return ext.minimumSdkVersion * 100000000 + ext.minimumScreenSize * 10000000 + ext.maximumScreenSize * 1000000 + ext.versionMajor * 10000 + ext.versionMinor * 100 + ext.versionPatch
}

private String generateVersionName() {
    String versionName = "${ext.versionMajor}.${ext.versionMinor}.${ext.versionPatch}"
    if (ext.versionClassifier == null) {
        if (ext.isSnapshot) {
            ext.versionClassifier = "SNAPSHOT"
        }
    }

    if (ext.versionClassifier != null) {
        versionName += "-" + ext.versionClassifier
    }
    return versionName
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.android.support.constraint:constraint-layout:1.1.0'
    implementation 'com.android.support:design:27.1.1'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    implementation 'com.squareup.retrofit2:retrofit:2.4.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.1.0'
    implementation 'com.google.guava:guava:25.1-android'
    implementation 'com.github.Carrene:helpdroid:v0.5.1'
    implementation 'org.jdeferred:jdeferred-android-aar:1.2.6'
    implementation 'com.github.mukeshsolanki:country-picker-android:1.2.4'
}