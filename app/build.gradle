apply plugin: 'com.android.application'
apply plugin: 'realm-android'
apply plugin: 'io.fabric'

ext.versionMajor = 00
ext.versionMinor = 28
ext.versionPatch = 00
ext.versionClassifier = ""
ext.isSnapshot = false
ext.minimumSdkVersion = 17
ext.minimumScreenSize = 1
ext.maximumScreenSize = 4

android {
    compileSdkVersion 27
    defaultConfig {
        applicationId "de.netalic.falcon"
        minSdkVersion project.ext.minimumSdkVersion
        targetSdkVersion 27
        versionCode generateVersionCode()
        versionName generateVersionName()
        flavorDimensions "default"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
    }
    sourceSets.test.resources.srcDirs += ["src/test/res"]

    signingConfigs {
        releaseSign {
            def propsFile = rootProject.file('keystore.properties')
            if (propsFile.exists()) {
                def props = new Properties()
                props.load(new FileInputStream(propsFile))
                storeFile = file(props['storeFile'])
                storePassword = props['storePassword']
                keyAlias = props['keyAlias']
                keyPassword = props['keyPassword']
            }
        }
    }

    buildTypes {
        debug {

            minifyEnabled false
            debuggable true
            useProguard false
            testCoverageEnabled = true
            proguardFiles getDefaultProguardFile('proguard-android.txt')
            proguardFiles fileTree('proguard').asList().toArray()

            applicationIdSuffix ".debug"
            resValue "string", "app_name", "falcon-debug"
        }
        release {
            signingConfig signingConfigs.releaseSign
            debuggable false
            minifyEnabled false

            proguardFiles getDefaultProguardFile('proguard-android.txt')
            proguardFiles fileTree('proguard').asList().toArray()
            applicationIdSuffix ".release"
            resValue "string", "app_name", "falcon-release"
        }
    }

    productFlavors {

        local {
            buildConfigField "String", "WEB_SERVICE_URL", "\"http://nightly.alpha.carrene.com\""
            buildConfigField "Integer", "WEB_SERVICE_VERSION", "1"
        }
        global {

            buildConfigField "String", "WEB_SERVICE_URL", "\"http://nightly.alpha.carrene.com\""
            buildConfigField "Integer", "WEB_SERVICE_VERSION", "1"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    configurations.all {
        resolutionStrategy.force 'org.hamcrest:hamcrest-core:1.3'
    }
}

private Integer generateVersionCode() {
    return ext.minimumSdkVersion * 100000000 + ext.minimumScreenSize * 10000000 + ext.maximumScreenSize * 1000000 + ext.versionMajor * 10000 + ext.versionMinor * 100 + ext.versionPatch
}

private String generateVersionName() {
    String versionName = "${ext.versionMajor}.${ext.versionMinor}.${ext.versionPatch}"
    if (ext.versionClassifier == null) {
        if (ext.isSnapshot) {
            ext.versionClassifier = "SNAPSHOT"
        }
    }

    if (ext.versionClassifier != null) {
        versionName += "-" + ext.versionClassifier
    }
    return versionName
}

dependencies {

    implementation fileTree(include: ['*.jar'], dir: 'libs')

    testImplementation "org.hamcrest:hamcrest-all:1.3"

    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.android.support:design:27.1.1'
    implementation 'com.android.support:cardview-v7:27.1.1'

    implementation 'com.squareup.retrofit2:retrofit:2.4.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.1.0'
    api 'com.google.guava:guava:26.0-android'

    implementation 'com.github.franmontiel:PersistentCookieJar:v1.0.1'
    implementation 'com.github.EhsanMashhadi:helpdroid:0.9.0'
    implementation 'com.github.mukeshsolanki:country-picker-android:1.2.4'
    implementation 'com.afollestad.material-dialogs:commons:0.9.6.0'
    implementation 'com.braintreepayments.api:drop-in:3.5.0'
    implementation 'com.github.florent37:viewtooltip:1.1.5'
    implementation('com.mikepenz:materialdrawer:5.9.5@aar') {
        transitive = true
        exclude group: 'com.android.support'
    }
    implementation 'info.hoang8f:android-segmented:1.0.6'
    implementation 'uk.co.chrisjenx:calligraphy:2.3.0'
    implementation 'com.andrognito.patternlockview:patternlockview:1.0.0'
    implementation 'com.google.zxing:core:3.2.1'
    implementation 'com.journeyapps:zxing-android-embedded:3.3.0@aar'
    implementation 'com.google.firebase:firebase-core:16.0.3'
    implementation 'com.crashlytics.sdk.android:crashlytics:2.9.5'
    implementation 'com.github.florent37:viewtooltip:1.1.5'
    implementation 'com.android.support:preference-v14:27.1.1'

    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.11.0'
    implementation 'ru.alexbykov:nopaginate:0.9.9'
    implementation 'jp.wasabeef:recyclerview-animators:2.3.0'
    implementation 'com.journeyapps:zxing-android-embedded:3.3.0@aar'
    implementation 'de.hdodenhof:circleimageview:2.2.0'

    debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.6.1'
    releaseImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.6.1'
    debugImplementation 'com.squareup.leakcanary:leakcanary-support-fragment:1.6.1'

    androidTestImplementation 'com.squareup.okhttp3:mockwebserver:3.10.0'
    androidTestImplementation "org.mockito:mockito-core:1.10.19"
    androidTestImplementation 'com.android.support.test:runner:1.0.2'

    testImplementation('junit:junit:4.12') {
        exclude group: 'org.hamcrest', module: 'hamcrest-core'
    }
    testImplementation 'com.squareup.okhttp3:mockwebserver:3.10.0'
    testImplementation("org.mockito:mockito-core:1.10.19")

    testImplementation("org.powermock:powermock-api-mockito:1.6.6") {
        exclude group: 'org.hamcrest', module: 'hamcrest-core'
    }
    testImplementation('org.powermock:powermock-module-junit4:1.6.6') {
        exclude group: 'org.hamcrest', module: 'hamcrest-core'
    }

    androidTestImplementation('com.android.support.test.espresso:espresso-core:3.0.2') {
        exclude group: 'org.hamcrest', module: 'hamcrest-core'
    }
    androidTestImplementation 'com.android.support.test:rules:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-intents:3.0.2'
    implementation "com.android.support.test.espresso:espresso-idling-resource:3.0.2"
    androidTestImplementation "com.android.support.test.espresso:espresso-contrib:3.0.2"


}

apply plugin: 'com.google.gms.google-services'
